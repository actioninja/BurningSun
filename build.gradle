buildscript {
    ext.kotlin_version = '1.1.2-2'
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "gradle plugins"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'com.matthewprenger.cursegradle'


if (System.getenv().BUILD_NUMBER != null) {
	ext.buildNumber = "${System.getenv().BUILD_NUMBER}"	
} else {
	ext.buildNumber = "custom"	
}

version = "${mc_version}-${mod_version}.${ext.buildNumber}"
group = mod_classpath
archivesBaseName = archiveBaseName

sourceCompatibility = targetCompatibility = "1.8" 
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}


minecraft {
    version = mc_version + "-" + forge_version
    runDir = "run"
    mappings = mapping_version
    replace '@VERSION@', project.version
    replaceIn "BurningSun"
}

repositories {
    maven {
        url "http://mvn.rx14.co.uk/shadowfacts/"
    }
    jcenter()
    mavenCentral()
    maven {
        name "JEI"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "FireBall API Depot"
        url "http://dl.tsr.me/artifactory/libs-release/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile group: "net.shadowfacts", name: "Forgelin", version: forgelin_version
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"

    compile files("lib/Baubles-1.11-1.4.4-deobf.jar")

}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

String getChangelogText() {
    def changelogFile = new File('changelog.txt')
    String str = ''
    String separator = '---'
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (lineCount < 3) {
            lineCount++
            if (it.startsWith(separator)) {return}
        }
        if (!it.startsWith(separator)) {
            str += "$it" + (lineCount < 3 ? ':\n\n' : '\n')
            return
        }
        done = true // once we go past the first version block, parse no more
    }
    return str
}

def curseRelations = {
    requiredLibrary 'shadowfacts-forgelin'
}

curseforge {
    if (System.getenv().CURSE_KEY != null)
        apiKey = System.getenv().CURSE_KEY

    project {
        id = "${curse_id}"
        changelog = getChangelogText()
        releaseType = 'beta'

        relations curseRelations
    }
}
